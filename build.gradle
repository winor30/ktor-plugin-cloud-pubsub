plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'maven-publish'
    id 'signing'
}

group = 'com.winor30'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.ktor:ktor-server-core:2.0.3'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.winor30'
            artifactId = 'ktor-plugin-cloud-pubsub'
            version = '0.0.2'
            from(components["java"])

            pom {
                url = 'https://github.com/winor30/ktor-plugin-cloud-pubsub'
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/winor30/ktor-plugin-cloud-pubsub"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    sign(publishing.publications["release"])
}
