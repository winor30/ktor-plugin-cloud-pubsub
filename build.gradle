plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'org.jetbrains.dokka' version "$dokka_version"
    id 'maven-publish'
    id 'signing'
}

group = 'com.winor30'
version = '0.0.3'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.ktor:ktor-server-core:2.0.3'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    dokkaJavadocPlugin "org.jetbrains.dokka:kotlin-as-java-plugin:$dokka_version"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.named("javadocJar") {
    from tasks.named("dokkaJavadoc")
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.winor30'
            artifactId = 'ktor-plugin-cloud-pubsub'
            version = '0.0.3'
            from(components["java"])

            pom {
                name = 'Ktor Plugin Cloud Pub/Sub'
                url = 'https://github.com/winor30/ktor-plugin-cloud-pubsub'
                description = 'A ktor plugin that Cloud Pub/Sub\'s push trigger messages into a defined type'
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
                developers {
                    developer {
                        id = 'winor30'
                        name = 'Takuma Katsumata'
                        email = 'tk.30.r.h@gmail.com'
                    }
                }

                scm {
                    connection = 'git@github.com:winor30/ktor-plugin-cloud-pubsub.git'
                    developerConnection = 'git@github.com:winor30/ktor-plugin-cloud-pubsub.git'
                    url = 'https://github.com/winor30/ktor-plugin-cloud-pubsub'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/winor30/ktor-plugin-cloud-pubsub"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    sign(publishing.publications["release"])
}
